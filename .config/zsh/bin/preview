#!/usr/bin/env zsh
filepaths=("$@")

if [ -z "${filepaths[*]}" ]; then
  return 1
fi

realpaths=()

for filepath in "${filepaths[@]}"; do
  # Expand `~` because bat can't expand it.
  realpaths+=("${filepath/#\~/${HOME}}")
done

bat_options=()
unique_extensions=("${(Mu@)realpaths%.*}")

for realpath in "${realpaths[@]}"; do
  case "${realpath}" in
    *.*)
      # Ignore the path if it includes dot(s).
      ;;
    *)
      # Add the path if it doesnâ€™t have any dot.
      unique_extensions+=("${realpath}")
      ;;
  esac
done

if [ "${#unique_extensions}" = "1" ]; then
  case "${unique_extensions[1]}" in
    .diff)
      delta < "${realpaths[@]}"
      exit
      ;;
    .jb | .rbs)
      bat_options+=(--language "ruby")
      ;;
  esac
fi

if [ "${#filepaths}" = "1" ]; then
  parts=("${(s;:;)realpaths[1]}")
  realpath="${parts[1]}"
  lineno="${parts[2]}"

  if [ -z "${lineno}" ]; then
    lineno="0"
  fi

  if file "${realpath}" | grep -E '\bimage data\b' -q; then
    if ((${+commands[chafa]})); then
      chafa --size 200x50 "${realpath}"
      return
    fi
  fi

  bat_options+=(
    --highlight-line "${lineno}"
  )

  if (("${bat_options[(I)--language]}" == 0)); then
    case "${realpath}" in
      .envrc.example)
        bat_options+=(--language "sh")
        ;;
      *Gemfile.local* | */pryrc | .pryrc | Steepfile)
        bat_options+=(--language "ruby")
        ;;
    esac
  fi

  case "${realpath}" in
    *.csv)
      if ((${+commands[nkf]})); then
        nkf "${realpath}" | bat "${bat_options[@]}" --file-name "${realpath}"
      else
        bat "${realpath}" "${bat_options[@]}"
      fi
      ;;
    *.diff)
      if ((${+commands[delta]})); then
        delta < "${realpath}"
      else
        bat "${realpath}" "${bat_options[@]}"
      fi
      ;;
    *)
      bat "${realpath}" "${bat_options[@]}"
  esac
else
  bat "${realpaths[@]}" "${bat_options[@]}"
fi
