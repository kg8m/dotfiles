#!/usr/bin/env zsh
source ~/.config/zsh/utility-functions.zsh

if [ ! -f "config/routes.rb" ]; then
  echo:error "\`config/routes.rb\` doesn't exist."
  return 1
fi

target_filepaths=("config/routes.rb")
target_candidate_filepaths=(
  ".env"
  ".envrc"
  ".ruby-version"
  "Gemfile"
  "Gemfile.lock"
)

for target_candidate_filepath in "${target_candidate_filepaths[@]}"; do
  if [ -f "${target_candidate_filepath}" ]; then
    target_filepaths+=("${target_candidate_filepath}")
  fi
done

if [ -d "config/routes" ]; then
  for target_filepath in config/routes/**/*.rb; do
    target_filepaths+=("${target_filepath}")
  done
fi

mkdir -p tmp/cache
result_filepath="tmp/cache/routes_result"
checksum_filepath="tmp/cache/routes_checksum"

if [ ! -f "${result_filepath}" ] ||
   [ ! -f "${checksum_filepath}" ] ||
   [ ! "${#target_filepaths}" = "$(wc -l "${checksum_filepath}" | awk '{ print $1 }')" ] ||
   ! shasum --check --quiet "${checksum_filepath}" >&2; then

  echo:info "Cache file for routing doesn't exist or the routing has been changed."
  execute_with_echo "shasum '${(j:' ':)target_filepaths}' > ${checksum_filepath}"
  execute_with_echo "bin/rails routes > ${result_filepath}"
fi

cat "${result_filepath}"
