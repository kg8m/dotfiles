#!/usr/bin/env zsh
source ~/.config/zsh/utility-functions.zsh

if [ ! -f "config/routes.rb" ]; then
  echo:error "\`config/routes.rb\` doesnâ€™t exist."
  return 1
fi

target_filepaths=("config/routes.rb")
target_filepaths+=("${(@f)$(fd --max-depth 1 --type f '^\.ruby-version$|^Gemfile\b')}")

if [ -d "config/routes" ]; then
  target_filepaths+=("${(@f)$(fd --type f '\.rb$' config/routes)}")
fi

mkdir -p tmp/cache
result_filepath="tmp/cache/routes_result"
checksum_filepath="tmp/cache/routes_checksum"

if [ ! -f "${result_filepath}" ] ||
   [ ! -f "${checksum_filepath}" ] ||
   [ ! -s "${result_filepath}" ] ||
   [ ! "${#target_filepaths}" = "$(wc -l "${checksum_filepath}" | awk '{ print $1 }')" ] ||
   ! shasum --check --quiet "${checksum_filepath}" >&2; then

  echo:info "The cache for routing is not available."

  # Build the cache before executing `rails routes` to avoid a race condition between cache building and `rails routes`.
  # Building the cache is fast, but `rails routes` is a slow command.
  eval_with_echo "shasum ${(@q-)target_filepaths} > ${(q)checksum_filepath}"
  eval_with_echo "bin/rails routes > ${(q)result_filepath}"
fi

cat "${result_filepath}"
