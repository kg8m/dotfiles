#!/usr/bin/env zsh
source ~/.config/zsh/utility-functions.zsh

command=("terminal-notifier")

title=""
message=""
host=""
is_stay="1"

while (("${#@}" > 0)); do
  case "$1" in
    --title)
      title="${2:?}"
      shift 2
      ;;
    --host)
      host="${2:?}"
      shift 2
      ;;
    --nostay)
      is_stay="0"
      shift 1
      ;;
    *)
      if [ -n "${message}" ]; then
        echo:error "Only 1 argument can be given except for options: ${message} vs. $1"
        return 1
      else
        message="$1"
        shift 1
      fi
      ;;
  esac
done

if [ -n "${title}" ]; then
  if [[ "${title}" =~ ^[-[] ]]; then
    title="\\${title}"
  fi

  command+=("-title" "${title}")
fi

if [ -z "${message}" ]; then
  message="Command finished."
fi

if [ -z "${host}" ]; then
  host="$(hostname)"
fi

command+=("-group" "NOTIFY_${title}_${host}_${message}")

if (($#host > 10)); then
  host="${host[1,10]}..."
fi

command+=("-message" "\\[${host}] ${message}")

if [ "${is_stay}" = "1" ]; then
  command+=("-sender" "TERMINAL_NOTIFIER_STAY")
fi

# `> /dev/null` for ignoring "Removing previously sent notification" message
ssh main -t "${(@q)command}" > /dev/null
