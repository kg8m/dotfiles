#!/usr/bin/env zsh
source ~/.zsh/my_functions.zsh

function git:add:bulk {
  local files=( $( git:status:filter:unstaged:with_untracked ) )

  if [ "${#files[@]}" -ne 0 ]; then
    execute_with_echo    "git add --intent-to-add -- ${files[*]}"
    execute_with_echo    "git diff -- ${files[*]}"
    execute_with_confirm "git add -- ${files[*]}"
    execute_with_echo    "git status"
  fi
}

function git:add:bulk:intent {
  local files=( $( git:status:filter:untracked ) )

  if [ "${#files[@]}" -ne 0 ]; then
    execute_with_echo "git add --intent-to-add -- ${files[*]}"
    execute_with_echo "git status"
  fi
}

function git:add:bulk:patch {
  local files=( $( git:status:filter:unstaged ) )

  if [ "${#files[@]}" -ne 0 ]; then
    execute_with_echo "git add --patch -- ${files[*]}"
    execute_with_echo "git status"
  fi
}

function git:restore:bulk {
  local files=( $( git:status:filter:unstaged ) )
  execute_with_echo    "git diff -R -- ${files[*]}"
  execute_with_confirm "git restore -- ${files[*]}"
  execute_with_echo    "git status"
}

function git:restore:bulk:staged {
  local files=( $( git:status:filter:staged ) )

  if [ "${#files[@]}" -ne 0 ]; then
    execute_with_echo    "git diff --staged -R"
    execute_with_confirm "git restore --staged -- ${files[*]}"
    execute_with_echo    "git status"
  fi
}

function git:switch:select {
  local current="$( git branch --show-current )"
  local branch="$( git:branch:filter:one "^${current} +" )"

  [[ -n "$branch" ]] && execute_with_echo "git switch $branch"
}

function git:update:with_log {
  local remote_branch="$( git rev-parse --abbrev-ref '@{upstream}' )"

  execute_with_echo "git --no-pager show --no-patch"
  execute_with_echo "git fetch"

  if [ "$( git rev-parse HEAD )" = "$( git rev-parse '@{upstream}' )" ]; then
    echo "Up to date."
  else
    execute_with_echo "git --no-pager log --reverse HEAD.. ${remote_branch}"
    execute_with_echo "git pull --stat --rebase"
  fi
}

function git:update:shallow {
  execute_with_echo "git pull --rebase --depth 1"
}

function git:update:master {
  local current_branch="$( git symbolic-ref --short HEAD )"

  execute_with_echo "git switch master"
  execute_with_echo "git update"
  execute_with_echo "git switch ${current_branch}"
}

function git:branch:create:with_set_upstream {
  local original_branch="$1"
  local branch_name="$2"
  local commit_message="$3"

  local command
  command+="git switch --create ${branch_name} ${original_branch}"
  command+=" && git commit --allow-empty --message=\"${commit_message}\""
  command+=" && git push --set-upstream origin ${branch_name}"
  command+="; git branches"

  execute_with_confirm "${command}"
}

function git:branch:delete:bulk:local {
  local branches=( $(
    git branch --verbose |
      egrep -v "${GIT_PROTECTED_LOCAL_BRANCH_PATTERN}" |
      filter |
      sed -e 's/^[* ]*//' |
      awk '{ print $1 }'
  ) )

  if [ "${#branches[@]}" -ne 0 ]; then
    execute_with_confirm "git branch --delete --force ${branches[*]}"
    execute_with_echo    "git branches"
  else
    echo "No target branches."
  fi
}

function git:branch:delete:bulk:remote {
  local branches=( $(
    git branch --verbose --remote |
      egrep -v "${GIT_PROTECTED_REMOTE_BRANCH_PATTERN}" |
      filter |
      awk '{ print $1 }' |
      sed -e 's#^origin/##'
  ) )

  if [ "${#branches[@]}" -ne 0 ]; then
    execute_with_confirm "git push --delete origin ${branches[*]}"
    execute_with_echo    "git branches"
  else
    echo "No target branches."
  fi
}

function git:branch:filter:one {
  local ignore_pattern="$1"
  local branches=( $( git:branch:all | egrep -v "${ignore_pattern}" | filter --no-multi --nth 1 --prompt "select a branch: " | awk '{ print $1 }' ) )

  if [[ ${#branches[@]} -ne 1 ]]; then
    echo "Select only 1 branch." >&2; exit 1
  else
    echo "${branches[1]}"
  fi
}

function git:branch:filter:remote:one {
  local prompt=${1:-"Select a branch: "}
  local branches=( $( git:branch:remote | filter --no-multi --prompt "${prompt}" ) )

  if [[ ${#branches[@]} -ne 1 ]]; then
    echo "Select only 1 branch." >&2; exit 1
  else
    echo "${branches[1]}"
  fi
}

function git:branch:all {
  local max_length="$( git branch --all --format '%(refname:strip=-1)' | git:proxy:wc --max-line-length )"
  local list_format="%(align:$(( max_length + 2 ))) %(refname:strip=-1) %(end) %(objectname:short) %(contents:subject)"

  git branch --all --format "${list_format}" |
    git:proxy:sed -e 's/^ \+//' |
    egrep -v "^HEAD +" |
    sort |
    awk '!x[$1]++'
}

function git:branch:remote {
  git branch --remote | awk '{ print $1 }'
}

function git:clone:shallow {
  execute_with_echo "git clone --single-branch --depth 1 $@"
}

function git:clean:bulk {
  local files=( $( git:status:filter:untracked ) )

  if [ "${#files[@]}" -ne 0 ]; then
    git:proxy:cat:files ${files[*]}
    execute_with_confirm "git clean --force -- ${files[*]}"
    execute_with_echo    "git status"
  fi
}

function git:log:compare:branch {
  local branch1=$( git:branch:filter:remote:one "select first branch: " )
  local branch2=$( git:branch:filter:remote:one "select another branch: "  )

  if [[ -n "$branch1" ]] && [[ -n "$branch2" ]]; then
    execute_with_confirm "git log $@ ${branch1}...${branch2}"
  else
    echo "Select 2 branches to compare." >&2; exit 1
  fi
}

function git:diff:branch {
  local branch1=$( git:branch:filter:remote:one "select first branch: " )
  local branch2=$( git:branch:filter:remote:one "select another branch: " )

  if [[ -n "$branch1" ]] && [[ -n "$branch2" ]]; then
    execute_with_confirm "git diff-without-space-changes $@ ${branch1}...${branch2}"
  else
    echo "Select 2 branches to compare." >&2; exit 1
  fi
}

function git:diff:branch:from {
  local target="$1"

  if [[ -n "$target" ]]; then
    local current="$( git rev-parse --abbrev-ref '@{upstream}' )"
    local rest_args="${@:2:($#-1)}"

    execute_with_confirm "git diff-without-space-changes ${rest_args} origin/${target}...${current}"
  else
    echo "Specify a branch name, e.g., \"master\"." >&2; exit 1
  fi
}

function git:diff:branch:from:nameonly {
  local target="$1"

  if [[ -n "$target" ]]; then
    local current="$( git rev-parse --abbrev-ref '@{upstream}' )"
    local rest_args="${@:2:($#-1)}"

    execute_with_confirm "git diff --name-only ${rest_args} origin/${target}...${current}"
  else
    echo "Specify a branch name, e.g., \"master\"." >&2; exit 1
  fi
}

function stash:all:with_message {
  execute_with_echo "git stash push --no-keep-index --include-untracked --message '$@'"
}

function git:status:filter:all {
  git status --short | git:status:utility:filter
}

function git:status:filter:staged {
  git status --short | egrep '^[^ ?]|^ A' | git:status:utility:filter
}

function git:status:filter:unstaged {
  git status --short | egrep '^[^?][^ A]' | git:status:utility:filter
}

function git:status:filter:unstaged:with_untracked {
  git status --short | egrep '^.[^ A]' | git:status:utility:filter
}

function git:status:filter:untracked {
  git status --short | egrep '^\?\?' | git:status:utility:filter
}

function git:status:utility:filter {
  filter --prompt "Select files: " | awk '{ print $2 }'
}

function git:submodule:update {
  execute_with_echo "git submodule update --remote --rebase"
  execute_with_echo "git status"
}

function git:submodule:remove:bulk {
  local targets=( $( git submodule | filter | awk '{ print $2 }' ) )

  for target in "${targets[@]}"; do
    execute_with_confirm "git submodule deinit $target"
    execute_with_confirm "git rm $target"
    execute_with_confirm "trash $target"
  done
}

function git:alias:list {
  git config --get-regexp '^alias\.' |
    git:proxy:sed -e 's/alias\.\([^ ]*\) \(.*\)/\1\t => \2/'
}

function git:utility:chmod {
  execute_with_echo "git update-index --add --chmod=$1"
}

function git:proxy:sed {
  if which gsed > /dev/null 2>&1; then
    gsed "$@"
  else
    sed "$@"
  fi
}

function git:proxy:wc {
  if which gwc > /dev/null 2>&1; then
    gwc "$@"
  else
    wc "$@"
  fi
}

function git:proxy:cat:files {
  local filepath
  local filepaths=( $@ )

  if which bat > /dev/null 2>&1; then
    bat --wrap --color=always --style=full ${filepaths[*]}
  else
    for filepath in ${filepaths[@]}; do
      echo "\n** File: ${filepath}\n"
      cat "${filepath}"
      echo
    done
  fi
}
