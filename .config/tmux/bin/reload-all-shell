#!/usr/bin/env zsh
set -e  # Stop on error
set -u  # Error for unknown variables

source ~/.config/zsh/utility-functions.zsh

if [ -z "${TMUX:-}" ]; then
  echo:error "Not in a tmux session."
  exit 1
fi

current_session_name="$(tmux display-message -p "#{session_name}")"
current_window_index="$(tmux display-message -p "#{window_index}")"

pane_info_set=("${(@f)$(
  tmux list-panes -a -F '#{session_name} #{window_index} #{pane_index} #{pane_current_command} #{pane_active}'
)}")

if [ -z "${pane_info_set[*]}" ]; then
  echo:error "There are no tmux sessions."
  exit
fi

# shellcheck disable=SC2034
for pane_info in "${pane_info_set[@]}"; do
  printf .

  pane_info_array=("${=pane_info}")

  session_name="${pane_info_array[1]}"
  window_index="${pane_info_array[2]}"
  pane_index="${pane_info_array[3]}"
  pane_current_command="${pane_info_array[4]}"
  pane_active="${pane_info_array[5]}"

  # Skip the current pane or non-zsh pane.
  if {
      [ "${session_name}" = "${current_session_name}" ] &&
      [ "${window_index}" = "${current_window_index}" ] &&
      [ "${pane_active}" = "1" ]
    }; then

    continue
  elif [ ! "${pane_current_command}" = "zsh" ]; then
    echo:warn "Non-zsh pane (${session_name}:${window_index}.${pane_index})"
    continue
  fi

  tmux respawn-pane -k -t "${session_name}:${window_index}.${pane_index}"
done

# Reload the current pane.
echo
execute_with_confirm tmux respawn-pane -k
