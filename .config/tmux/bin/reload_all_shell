#!/usr/bin/env zsh
set -e  # Stop on error
set -u  # Error for unknown variables

source ~/.config/zsh/utility-functions.zsh

if [ -z "${TMUX:-}" ]; then
  echo:error "Not in a tmux session."
  exit 1
fi

pane_info_format="#{session_name}:#{window_index}.#{pane_index}"
pane_info_filter_non_active="#{||:#{==:#{window_active},0},#{==:#{pane_active},0}}"

zsh_pane_info_list=("${(@f)$(
  pane_info_filter="#{&&:${pane_info_filter_non_active},#{==:#{pane_current_command},zsh}}"
  tmux list-panes -a -F "${pane_info_format}" -f "${pane_info_filter}"
)}")
non_zsh_pane_info_list=("${(@f)$(
  pane_info_filter="#{&&:${pane_info_filter_non_active},#{!=:#{pane_current_command},zsh}}"
  tmux list-panes -a -F "${pane_info_format} with #{pane_current_command}" -f "${pane_info_filter}"
)}")

# Reload non-active zsh panes.
if [ -n "${zsh_pane_info_list[*]}" ]; then
  for pane_info in "${zsh_pane_info_list[@]}"; do
    printf .
    tmux respawn-pane -k -t "${pane_info}"
  done
  echo
fi

# Show warnings for non-zsh panes.
if [ -n "${non_zsh_pane_info_list[*]}" ]; then
  for pane_info in "${non_zsh_pane_info_list[@]}"; do
    echo:warn "Non-zsh pane (${pane_info})"
  done
fi

# Reload the current pane.
echo
execute_with_confirm tmux respawn-pane -k
